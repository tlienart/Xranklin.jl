<!--

Last edit: Feb 15

* âœ… PyPlot.jl
* âœ… Plots.jl
* âœ… CairoMakie
* âŒ GLMakie      NOTE: not tried
* âœ… WGLMakie
* âœ… PGFPlotsX
* âœ… PlotlyJS
* âœ… Gaston
* âœ… UnicodePlots
* âœ… Gadfly
* ðŸš§ GLEPlot

 -->

+++
showtoc = true
header = "Plots"
menu_title = header
+++

~~~
<style>
img.code-figure {
  max-width: 600px;
  min-width: 450px;
}

.code-stdout {
  visibility: hidden;
}
</style>
~~~

## Overview

Julia has many packages that offer plotting capacity and you can use any of them in Franklin as long as the resulting plot is showable as SVG or PNG or has a [custom show](/syntax/code/#custom_show) method.

One key difficulty is that if you want your site to be built remotely (e.g. by [GitHub Action][GA]) then you need to ensure that the relevant dependencies (if any) are installed to enable this.
We discuss this for each of the plotting package below.
Remember that you also **must** add the relevant plotting library to your website environment using [Pkg.jl](https://github.com/JuliaLang/Pkg.jl) e.g. with something like

```julia
using Pkg; Pkg.add("Plots")
```

\note{
  If you use another approach to remotely build your website, for instance with GitLab, please consider opening an issue discussing how to adapt the GitHub-specific instructions. It should be fairly similar.
}

In order to avoid name clashes, all packages in the code snippets below are `import`ed and so all function calls are of the form `Plots.plot`.
You don't need to do this if you're using a single plotting library of course.

### Overhead

In the table below we list the time taken from starting a GA deployment to displaying a meaningful plot.
The time includes the installation of dependencies, the installation of the plotting package and the time it takes to execute the code.
The exact time you get in your case will depend on GA and so you should take that number as an indicator rather than an exact figure.
Note also that this time is a one-off cost, subsequent plots should take negligible time in comparison.

\lskip

| Plotting package | Result | Time (min) |
| ---------------- | ------ | ---------- |
| [Plots.jl](#plots.jl) (GR backend)  | [link](/ttfx/plots/)        | {{ttfx plots}}        |
| [CairoMakie.jl](#cairomakie.jl)     | [link](/ttfx/cairomakie/)   | {{ttfx cairomakie}}   |
| [WGLMakie.jl](#wglmakie.jl)         | [link](/ttfx/wglmakie/)     | {{ttfx wglmakie}}     |
| [PyPlot.jl](#pyplot.jl)             | [link](/ttfx/pyplot/)       | {{ttfx pyplot}}       |
| [PGFPlots.jl](#pgfplots.jl)         | [link](/ttfx/pgfplots/)     | {{ttfx pgfplots}}     |
| [PGFPlotsX.jl](#pgfplotsx.jl)       | [link](/ttfx/pgfplotsx/)    | {{ttfx pgfplotsx}}    |
| [PlotlyJS.jl](#plotlyjs.jl)         | NA                          | NA                    |
| [Gaston.jl](#gaston.jl)             | [link](/ttfx/gaston/)       | {{ttfx gaston}}       |
| [UnicodePlots.jl](#unicodeplots.jl) | [link](/ttfx/unicodeplots/) | {{ttfx unicodeplots}} |
| [Gadfly.jl](#gadfly.jl)             | [link](/ttfx/gadfly/)       | {{ttfx gadfly}}       |
| [GLEPlot.jl](#gleplot.jl)           | [link](/ttfx/gleplot/)      | {{ttfx gleplot}}      |

\lskip

A final note here is that if the pages on which you have plots don't change and that you use the cache, these pages will be skipped at build time and you won't have to pay the full overhead (only the installation of the dependencies but that's always under 1 min).

### Suppressing stdout

In _some_ cases the usage of a plotting package may lead to the first plot outputting a load of information to stdout.
For instance, if you decide to use PyPlot with Conda instead of the approach [suggested below](#pyplot_with_ga), information about the installation of miniconda will be shown to stdout for the first plot.
This would ultimately appear on your site as well.

To help suppress this, Franklin adds a class `fig-stdout` to the stdout div generated by the execution of a cell that produces a result showable as a figure.
In short, if you see that one of your plot shows some stdout you don't want on your website, you can put something like this in your CSS:

```css
.fig-stdout { visibility: hidden; height: 0; margin: 0; }
```

If you use one of the packages mentioned below as well as the suggested instructions, this should not be necessary.

## Plots.jl

[Plots.jl](https://github.com/JuliaPlots/Plots.jl) is one of the most common plotting package used.
It is pretty easy to use and has [great documentation](https://docs.juliaplots.org/stable/).
The default backend ([GR.jl](https://github.com/jheinen/GR.jl)) works pretty well and is fairly quick.

Relative to Franklin, objects plotted via functions like `Plots.plot` are showable to SVG
so it's particularly simple to use this plotting library with Franklin.

\lskip

\showmd{
  ```!
  # name: plots
  import Plots

  x = range(0, pi, length=500)
  y = @. sin(exp(x)) * sinc(x)

  Plots.plot(x, y, label="Hello", size=(500, 300))
  ```
}

### Plots with GA

The GR backend requires to have `qt5-default` installed and to run the Julia command with [`xvfb`](https://en.wikipedia.org/wiki/Xvfb).
So you will need to have a line in your GitHub Action script like

```yml
run: |
     sudo apt-get update -qq
     sudo apt-get install -y qt5-default
```

and you will also need to prefix the call to Julia for the actual website building with `xvfb-run`:

```yml
run: xvfb-run julia -e 'using Pkg; ...'
```

The overhead of installing `qt5-default` on GA is a bit under 30s, and the time to precompile the Plots package and get the first plot on GA is around 1 min at the time of writing.

Remember to also add `Plots` to the site environment.


## CairoMakie

CairoMakie is the Cairo backend for [Makie.jl](https://github.com/JuliaPlots/Makie.jl).
It is geared towards high-quality 2D plotting.
See also the Franklin-based [Makie documentation](https://makie.juliaplots.org/stable/).

\lskip

\showmd{
```!
  # name: cairomakie
  import CairoMakie
  CairoMakie.activate!()

  x  = range(0, 10, length=100)
  y1 = sin.(x)
  y2 = cos.(x)

  CairoMakie.scatter(x, y1,
    color      = :red,
    markersize = range(5, 15, length=100)
  )
  CairoMakie.scatter!(x, y2,
    color    = range(0, 1, length=100),
    colormap = :thermal
  )

  CairoMakie.current_figure()
  ```
}

The `current_figure()` at the end returns an object showable as SVG which is what is displayed above.

For many more examples using this package, see the wonderful [Beautiful Makie](https://lazarusa.github.io/BeautifulMakie/)
site by [Lazaro Alonso](https://github.com/lazarusA) and also based on Franklin.

### CairoMakie with GA

You don't need to install anything specific in your GA script but remember to add `CairoMakie` to the site environment.

## WGLMakie

WGLMakie is the WebGL backend for [Makie.jl](https://github.com/JuliaPlots/Makie.jl).
See also the [docs](https://makie.juliaplots.org/dev/documentation/backends/wglmakie/).

Combined with [JSServe.jl](https://github.com/SimonDanisch/JSServe.jl) it can produce HTML with Javascript for interactive plots.

(Safari users will need to enable WebGL, see [link in the WGLMakie docs](https://makie.juliaplots.org/stable/documentation/backends/wglmakie/#troubleshooting))

\lskip

\showmd{
  ```!wgl
  # name: wglmakie
  import WGLMakie, JSServe
  WGLMakie.activate!()

  <|(io, o) = show(io, MIME"text/html"(), o)

  io = IOBuffer()

  io <| JSServe.Page(exportable=true, offline=true)
  io <| WGLMakie.scatter(1:4)
  io <| WGLMakie.surface(rand(4,4))
  io <| JSServe.Slider(1:3)

  String(take!(io))
  ```

  \htmlshow{wgl}
}

### WGLMakie with GA

You don't need to install anything specific in your GA script but remember to add `WGLMakie` to the site environment.

## PyPlot.jl

[PyPlot.jl](https://github.com/JuliaPy/PyPlot.jl) is an interface to [matplotlib](https://matplotlib.org/) and is a great option if you're already familiar with that.

\lskip


\showmd{
  ```!
  # name: pyplot
  import PyPlot

  x = range(0, pi, length=500)
  y = @. sin(exp(x)) * sinc(x)

  PyPlot.figure(figsize=(6, 4))
  PyPlot.plot(x, y, lw=3, label="Hello")
  PyPlot.legend()
  PyPlot.gcf()
  ```
}

Note how we need to use `gcf()` here, it returns a figure object which is showable as SVG (if you don't use it, nothing will be shown).

### PyPlot with GA

PyPlot relies on [PyCall.jl](https://github.com/JuliaPy/PyCall.jl) which relies on [Conda.jl](https://github.com/JuliaPy/Conda.jl) to link to the appropriate Python libraries.
It's quite tricky to get this whole machinery working fine with GA, the recommended way to do so is to use `setup-python` e.g.:

```yml
uses: actions/setup-python@v2
with:
  python-version: 3.8
```

install matplotlib (plus any other package you would call via PyCall separately) using `pip` and exctract the exact path to the python binary using `which`:

```yml
run: |
     pip install matplotlib
     export PYTHON=$(which python)
```

and finally start the Julia command with a build of `PyCall` to ensure it uses that `PYTHON` path:

```yml
run: julia -e '
       using Pkg; Pkg.add("PyCall"); Pkg.build("Pycall");
       ...'
```

this will guarantee that the right executable is used even when you're using the GA cache.

Remember to also add PyPlot to the site environment.

## PGFPlots.jl

[PGFPlots.jl](https://github.com/JuliaTeX/PGFPlots.jl) is an interface to [PGFPlots](https://www.ctan.org/pkg/pgfplots)
and produces LaTeX-style plots.

\lskip

\showmd{
  ```!
  # name: pgfplots
  using LaTeXStrings
  import PGFPlots
  x = range(0, pi, length=500)
  y = @. sin(exp(x)) * sinc(x)
  PGFPlots.Axis(
    PGFPlots.Plots.Linear(x, y, style="smooth, no marks"),
    xlabel = L"x",
    ylabel = L"f(x) = \sin(\exp(x))\mathrm{sinc}(x)"
  )
  ```
}

### PGFPlots with GA

In order to use this package on GA you need a minimal texlive installation.
This requires adding the following to your GA script:

```yml
run: |
  sudo apt-get update -qq
  sudo apt install -y pdf2svg texlive-latex-base texlive-binaries texlive-pictures texlive-latex-extra texlive-luatex
```

Remember to also add PGFPlots to the site environment.

## PGFPlotsX.jl

[PGFPlotsX.jl](https://github.com/KristofferC/PGFPlotsX.jl) is also an interface to [PGFPlots](https://www.ctan.org/pkg/pgfplots) and produces LaTeX-style plots but it does have much fewer dependencies than PGFPlots and its syntax is much closer to PGFPlots.

\lskip

\showmd{
  ```!
  # name: pgfplotsx
  using LaTeXStrings
  import PGFPlotsX
  x = range(0, pi, length=500)
  y = @. sin(exp(x)) * sinc(x)
  PGFPlotsX.@pgf PGFPlotsX.Axis(
      {
        xlabel = L"x",
        ylabel = L"f(x) = \sin(\exp(x))\mathrm{sinc}(x)"
      },
      PGFPlotsX.Plot(
        {no_marks},
        PGFPlotsX.Table(x, y)
      )
  )
  ```
}

### PGFPlotsX with GA

Same as for PGFPlots (see [above](#pgfplots_with_ga)).
Remember to also add PGFPlotsX to the site environment.

## PlotlyJS

[PlotlyJS.jl](https://github.com/JuliaPlots/PlotlyJS.jl) is an interface to [Plotly](https://plotly.com/javascript/) and allows embedding semi-interactive plots.
The code below separates concerns a bit by having a custom script loading the JSON produced by PlotlyJS.
You could easily hide most of this logic in a lxfun or hfun.

The overall structure is

1. a Javascript `PlotlyJS_json` function which can load a JSON representation of a Plotly plot (this could be put in your `_layout/foot.html`),
1. the PlotlyJS code to plot what you want,
1. an empty div block with an id that can be pointed to,
1. some Javascript calling the `PlotlyJS_json` function.

\lskip

\showmd{
  ~~~
  <script src="/libs/plotly/plotly.min.js"></script>
  <script>
      const PlotlyJS_json = async (div, url) => {
        response = await fetch(url)
        fig = await response.json()
        if (typeof fig.config === 'undefined') { fig["config"]={} }
          delete fig.layout.width
          delete fig.layout.height
          fig["layout"]["autosize"] = true
          fig["config"]["autosizable"] = true
          fig["config"]["responsive"] = true
          fig.config["scrollZoom"] = false
          delete fig.config.staticPlot
          delete fig.config.displayModeBar
          delete fig.config.doubleClick
          delete fig.config.showTips
          Plotly.newPlot(div, fig);
      }
  </script>
  ~~~

  ```!
  # name: plotlyjs
  import PlotlyJS
  p=PlotlyJS.plot(
      PlotlyJS.scatter(x=1:10, y=rand(10), mode="markers"),
      PlotlyJS.Layout(
        title="Responsive Plots"
      )
  )
  opath = mkpath(joinpath(Utils.path(:site), "assets", "plotlyfigs"))
  PlotlyJS.savejson(p, joinpath(opath, "ex.json"));
  ```

  ~~~
  <div id="foobar"></div>

  <script>
  graphDiv = document.getElementById("foobar");
  plotlyPromise = PlotlyJS_json(graphDiv, "/{{> ifelse($_final, $base_url_prefix, "" ) }}assets/plotlyfigs/ex.json")
  </script>
  ~~~
}

### PlotlyJS with GA

It doesn't require anything specific in your GA. Make sure the Javascript library is in your `/libs/` though, along with the PlotlyJS package in the site environment.


## Gaston.jl


[Gaston.jl](https://github.com/mbaz/Gaston.jl) is an interface to [GnuPlot](http://gnuplot.sourceforge.net).
It is a great choice if you're familiar with GnuPlot. There is also very good [documentation](https://mbaz.github.io/Gaston.jl/stable/).
Note that, by default, PNG images will be generated.

\lskip

\showmd{
  ```!
  # name: gaston
  import Gaston
  Gaston.set(term="qt")
  Gaston.set(termopts="font 'Helvetica,14'")
  x = range(0, pi, length=500)
  y = @. sin(exp(x)) * sinc(x) + 1
  Gaston.plot(x, y)
  ```
}


### Gaston with GA

You need to install GnuPlot and that's it:

```
run: |
  sudo apt-get update -qq
  sudo apt-get install -y gnuplot
```

Remember to also add Gaston to the site environment.


## UnicodePlots.jl

[UnicodePlots.jl](https://github.com/JuliaPlots/UnicodePlots.jl) is a plotting library produced text-based plots.
This is meant for use in the REPL but you can actually generate ANSI text with it which can, in turn, be converted to HTML that can be injected in Franklin.
This requires a custom show method which depends on a Python library [ansi2html](https://github.com/pycontribs/ansi2html/).

\showmd{
  ```!
  # name: unicodeplots
  import UnicodePlots
  x = range(0, pi, length=500)
  y = @. sin(exp(x)) * sinc(x)
  UnicodePlots.lineplot(x, y,
    xlim=[0, pi],
    ylim=[-0.2, 1],
    border=:dotted
  )
  ```
}

The code to insert in your `utils.jl` to get the relevant custom show method is

```julia
import UnicodePlots

function html_show(p::UnicodePlots.Plot)
    td = tempdir()
    tf = tempname(td)
    io = IOBuffer()
    UnicodePlots.savefig(p, tf; color=true)
    # assume ansi2html is available
    if success(pipeline(`cat $tf`, `ansi2html -i -l`, io))
        return "<pre>" * String(take!(io)) * "</pre>"
    end
    return ""
end
```

It requires `ansi2html`, a Python library which you can install via `pip`.

### UnicodePlots with GA

This is similar to the case of PyPlot: you need to install ansi2html which can be done with

```yml
uses: actions/setup-python@v2
with:
  python-version: 3.8
run: pip install ansi2html
```

as well as adding UnicodePlots to the site environment.

## Gadfly

[Gadfly.jl](https://github.com/GiovineItalia/Gadfly.jl) is a plotting system written in Julia and influenced by the Grammar of Graphics.

\lskip

\showmd{
  ```!
  # name: gadfly
  import Gadfly
  x = range(0, pi, length=500)
  y = @. sin(exp(x)) * sinc(x)
  Gadfly.plot(x=x, y=y, Gadfly.Geom.path)
  ```
}

### Gadfly with GA

Gadfly doesn't require anything specific in your GA script, just remember to add it to the site environment.

## GLEPlot

[GLEPlot.jl](https://github.com/tlienart/GLEPlot.jl) is an **experimental** wrapper around the
[Graphics Layout Engine](https://glx.sourceforge.io), a mature library geared around producing publication quality plots.

For now GLEPlot is not ready yet for consumption by general users.

### GLEPlot with GA

```yml
run: |
    curl -L https://sourceforge.net/projects/glx/files/gle4%20%28Current%20Active%20Version%29/4.3.1/gle-4.3.1-Linux.zip/download > gle.zip
    unzip gle.zip
```

on CI you will also have to indicate the path to the binary explicitly, so in `utils.jl` add

```
ENV["GLE"] = "gle-4.3.1-Linux/bin/gle"
```
